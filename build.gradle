//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
	id "maven-publish"
	alias libs.plugins.loom
	alias libs.plugins.minotaur
}

version = "$baseVersion+$branch"
archivesBaseName = project.archivesName

repositories {
	maven { url "https://maven.nucleoid.xyz/" }
	maven { url "https://maven.neoforged.net/releases/" }
	maven { url "https://maven.su5ed.dev/releases" }
	maven { url "https://repo.sleeping.town/" }
}

dependencies {
	minecraft libs.mc
	mappings loom.layered {
		it.mappings variantOf(libs.yarn) { classifier "v2" }
		it.mappings libs.neoYarn
	}

	neoForge libs.neo
	modImplementation libs.ffapi

	modImplementation libs.placeholder

	include libs.placeholder
}

loom {
	accessWidenerPath = file("src/main/resources/glowcase.accesswidener")
}

processResources {
	final Map<String, String> meta = [
		version       : version,
		modId         : modId,
		modName       : modName,
		modDescription: modDescription,
		homepage      : homepage,
		issues        : issues,
		sources       : sources,
		license       : license,
		authors       : "${authors}. Contributions by ${contributors}",
		mc            : libs.versions.mc.get(),
		neo           : libs.versions.neo.get(),
		fapi          : libs.versions.fapi.get(),
		placeholder   : libs.versions.placeholder.get()
	]
	inputs.properties meta
	filesMatching("META-INF/neoforge.mods.toml") { expand meta }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = 'glowcase'
	dependencies {
		required.version "fabric-api", libs.versions.fapi.get()
		embedded.version "placeholder-api", libs.versions.placeholder.get()
	}
	changelog = System.getenv("CHANGELOG")
	syncBodyFrom = "<!--DO NOT EDIT MANUALLY: synced from gh readme-->\n" + rootProject.file("README.md").text
	uploadFile = remapJar
}
